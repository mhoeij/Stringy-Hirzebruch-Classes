# To run this file in Maple, first load the file "MapleCode".
# (the file RunAll will load+run all files including thise one).


# SU(3) un-normalized #############################################################

F := ((1+y*exp(-z))*z)/(1-exp(-z));

X2 := (  subs(z=E[2],F)*subs(z=U[2]-E[2],F)*subs(z=V[2]-E[2],F)*(3*H+6*L-2*E[1]-E[2])  )/
     (  limit(F,z=0)*subs(z=3*H+6*L-2*E[1]-E[2],F)*subs(z=U[2],F)*subs(z=V[2],F)  );
X1 := (  subs(z=E[1],F)*subs(z=U[1]-E[1],F)*subs(z=V[1]-E[1],F)*subs(z=W[1]-E[1],F)*subs(z=H,F)  )/
     (  limit(F,z=0)*subs(z=W[1],F)  );

C := X2*X1;

	# Note: C is a product of various substitutions in F.  Most of these substitutions are harmless,
	# except subs(z=0, F) which produces 0/0. That's why we have to replace that factor by limit(F, z=0).

C2 := BlowUp(E[2], {U[2],V[2]}, C):
C2 := subs(U[2]=H+3*L-E[1],V[2]=E[1],C2):

C1 := BlowUp(E[1], {U[1],V[1],W[1]}, C2):
C1 := subs( U[1] = H + 2*L, V[1] = H + 3*L, W[1] = S, C1):

Q[SU3_not_normalized] := PiStar(C1, [H, H + 2*L, H + 3*L]):

# Equivalent shorter expression:
#  -(1+y)*((3*y-1)*exp(S)+y^2-3*y)/(y+exp(S)) - (1+y)^2*exp(S+L)*((1+y)*(exp(4*L)-y*exp(2*S))+y*(exp(S+L)*(exp(2*L)+exp(S)-1)-exp(3*L)))/(exp(6*L)+exp(3*S)*y)/(y+exp(S)) ;




# SU(3) normalized ################################################################

G := (z*(1+y))/(1-exp(-z*(1+y)))-y*z;

X2 := (  subs(z=E[2],G)*subs(z=U[2]-E[2],G)*subs(z=V[2]-E[2],G)*(3*H+6*L-2*E[1]-E[2])  )/
     ( subs(z=3*H+6*L-2*E[1]-E[2],G)*subs(z=U[2],G)*subs(z=V[2],G)  );
X1 := (  subs(z=E[1],G)*subs(z=U[1]-E[1],G)*subs(z=V[1]-E[1],G)*subs(z=W[1]-E[1],G)*subs(z=H,G)  )/
     (  subs(z=W[1],G)  );

C := X2*X1;

C2 := BlowUp(E[2], {U[2],V[2]}, C):
C2 := subs(U[2]=H+3*L-E[1],V[2]=E[1],C2):

C1 := BlowUp(E[1],{U[1],V[1],W[1]},C2):
C1 := subs(U[1] = H + 2*L, V[1] = H + 3*L, W[1] = S, C1):

Q[SU3] := PiStar(C1, [H, H + 2*L, H + 3*L]):

# Equivalent shorter expression:
# 1-3*y+2*y*(y+1)/(exp(S*(y+1))+y) -(y+1)*exp((y+1)*(S+L))*(-exp(2*S*(y+1))*y^2+(-1+exp(L*(y+1)))*(exp(2*L*(y+1))+exp(S*(y+1)))*(exp(L*(y+1))+exp(S*(y+1)))*y+exp(4*L*(y+1)))/(exp(S*(y+1))+y)/(exp(6*L*(y+1))+exp(3*S*(y+1))*y);
